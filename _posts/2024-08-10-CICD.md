---
layout: post
title: 젠킨스로 SpringBoot + Vue 애플리케이션 CICD 파이프라인 구성해보기 - Version1
tags:
  - [CICD]
---

<br>

현재 내가 속한 팀에서는 닷넷,c#으로 서비스를 개발하고 있다. 최근 차세대 프로젝트를 시작하면서 springboot+vue.js 로 작은 서비스를 개발하기 시작했다. 그래서 젠킨스를 사용해서 아주아주 간단한 springboot+vue.js 애플리케이션을 배포하는 파이프라인을 구성해보았다. 

스프링부트를 사용해서 `/api/hello` 경로로 요청이 들어올때 간단한 인사말을 리턴해주는 Rest Controller를 개발했다. 그리고 Vue.js를 사용해서 백엔드 API 서버에서 받아온 간단한 인사말을 출력해주는 index.html 페이지를 개발했다. 이제 배포 프로세스를 살펴보자.

<br>

### 0. 사용한 서비스

`AWS EC2(Ubuntu)`

 : 젠킨스서버 , 프론트엔드/백엔드 배포서버로 사용

`docker`

: 젠킨스, 프론트엔드/백엔드 서비스를 도커 컨테이너로 구성

`Jenkins` 

: 젠킨스 파이프라인으로 CICD 구성

`Nginx`

: 1) Vue.js 프로젝트에서 빌드된 정적 결과물을 서비스해주는 Web Server로 사용

: 2) /api 경로로 요청이 들어왔을 때 서비스 중인 백엔드 컨테이너로 요청을 던지는 Reverse Proxy 로 사용

`Tomcat`

: 스프링부트 프로젝트 빌드 결과물 (jar)을 서비스해주는 WAS (Web Application Server)

`Gitlab`

: 깃랩 리모트 서버에서 push 이벤트가 발생할 때, Webhook을 사용해서 젠킨스에 빌드 요청

<br><br>

### 1. EC2 서버 구성

이번 버전에서는 간단하게 한대의 EC2 우분투 서버 안에서 젠킨스, tomcat (jar 파일 실행시 내장톰캣 동작하는 방식), nginx를 도커 컨테이너로 구성했다. 격리된 각각의 서비스끼리 통신하고 파이프라인 작업을 수행하기 위해 SSH 프로토콜을 사용했다. 추후에 각각의 서비스를 서로 다른 서버로 분리 하더라도 SSH 통신할 때 사용한 IP 주소만 변경하면 쉽게 구성을 확장할 수 있을 것 같다. 

<img src="https://github.com/duohui12/blog/blob/main/images/cicd/ec2_setting.png?raw=true" alt="ec2_setting.png" style="zoom:35%;" />

젠킨스와 톰캣을 기본적으로 8080 포트를 사용한다. 각각의 컨테이너 내부는 격리된 환경이기 때문에 컨테이너 내부에서 젠킨스와 톰캣 모두 8080 포트를 사용해도 문제가 없다. 하지만 한대의 EC2 서버에서는 중복된 포트를 사용하지 않기 위해 젠킨스 서비스는 18080:8080으로, 톰캣은 8080:8080으로 포트포워딩 시켰다.  

> 포트포워딩
>
> 포트 포워딩은 호스트 머신에서 도커 컨테이너 내부의 서비스(예: 젠킨스,톰캣)에 접근할 수 있도록 호스트와 컨테이너 간의 네트워크 포트를 연결해주는 기능이다. (ex. 18080:8080 => 호스트에서 18080 포트로 접근시 특정 컨테이너 내부의 8080 포트로 연결해준다)

<br>

사실 이번에는 개인 공부용으로 CICD를 구성해본거라서 EC2 서버 구성시 프리티어로 인스턴스를 생성했다. 그런데 이 서버에서 젠킨스를 도커 컨테이너로 실행시키는 것까지는 성공했는데, 깃랩에서 소스를 당겨와 빌드하는 과정에서 서버가 먹통이 되어버리는 현상이 계속 발생했다. 처음에는 파이프라인 스크립트를 잘못 작성했거나 환경 설정의 문제인줄 알아서 삽질을 많이 했다.. 찾아보니 프리티어 인스턴스는 램이 1기가 뿐인데 젠킨스 빌드 과정에서 램을 1기가 이상 사용하면서 서버가 뻗어버리는 문제였다. 이럴때 AWS에서 램 용량을 늘릴수도 있지만, 그러면 프리티어 서비스를 사용할 수 없다. 이럴때는 리눅스의 하드디스크에 swap area를 설정해서 가상메모리로 사용하는 방법이 있다. (설정 방법은 검색하면 친절하게 명령어가 나와서 그대로 따라했다)

<br><br>

### 2. Nginx를 웹서버, 리버스 프록시 서버로 사용하기

이제 서버구성을 했으니 사용자가 http로 요청을 보낼때 어떻게 프론트엔드/백엔드에서 응답을 해주는지 살펴보자. 

<div>
  <div style="float:left; width:50%;">
    <img src="https://github.com/duohui12/blog/blob/main/images/cicd/nginx_process2.png?raw=true">
  </div>
  <div style="float:left; width:50%;">
    <img src="https://github.com/duohui12/blog/blob/main/images/cicd/nginx_conf_green.png?raw=true"> 
  </div>
</div>
<div style="clear:both">  
</div>


먼저 이 서버에서는 NGINX가 80번 포트를 사용한다. 따라서 사용자가 http로 요청을 보내면 가장 먼저 NGINX가 요청을 받게된다. 

`http://mydomain/api/hello` 처럼 경로가 `/api/*` 패턴인 요청이 들어오면 NGINX는 8080번 포트로 서비스 중인 톰캣으로 요청을 전달한다. 즉, NGINX가 `Reverse Proxy` 의 역할을 하게 된다. 

> Reverse Proxy
>
> 인터넷과 서버 사이에 위치해서 요청을 중개한다. 인터넷에서 들어온 요청을 리버스 프록시 서버에서 먼저 받은 후, 결과를 응답해 줄 실제 서버로 요청을 전달한다. 

경로가 `/api/*` 패턴이 아닌 나머지 모든 요청은 (`/*`) NGINX 내부의 특정 경로에서 서비스 해준다. 나의 경우 `green-vue-app` 경로에 Vue.js 프로젝트를 빌드한 정적 결과물을 (css, html, js) 저장해 두었다. 즉, NGINX가 `Web Server` 역할을 하게 된다.

<br><br>

### 3. BLUE/GREEN 배포 

무중단 배포 구현을 위해 BLUE/GREEN 배포 방식을 선택했다. 

> BLUE/GREEN 배포
>
> 무중단 배포를 위한 전략 중 하나이다. 블루환경은 현재 프로덕션에서 실행중인 애플리케이션 버전으로, 모든 사용자 트래픽이 이 환경으로 라우팅 된다. 그린환경은 새로운 애플리케이션 버전을 배포하는 환경으로 배포 및 테스트가 완료되면 사용자 트래픽을 이 환경으로 전환한다. 
>
> 이 방식은 새로운 애플리케이션이 정상적으로 동작하는지 그린환경에서 테스트를 마친 후에 트래픽을 전환하므로 위험을 최소화 할 수 있고, 그린환경으로 트래픽을 전환한 후에 문제가 발생할 경우 빠르게 블루버전으로 롤백할 수 있다는 장점이 있다. 하지만, 두개의 환경을 동시에 유지해야하기 때문에 하드웨어 자원이나 클라우드 인스턴스가 두 배로 필요할 수 있다는 단점이 있다. 

<br>

<div>
  <div style="float:left; width:50%;">
    <img src="https://github.com/duohui12/blog/blob/main/images/cicd/blue.png?raw=true">
  </div>
  <div style="float:left; width:50%;">
    <img src="https://github.com/duohui12/blog/blob/main/images/cicd/green.png?raw=true"> 
  </div>
</div>
<div style="clear:both">  
</div>


왼쪽 이미지는 현재 블루환경이 운영중이고, 모든 트래픽은 블루환경으로 향하고 있음을 보여준다. 이 상태에서 최신 버전의 애플리케이션을 그린환경에 구성한다. 그린환경을 테스트한 후에, 오른쪽 이미지처럼 모든 트래픽이 그린환경으로 향하도록 nginx 설정을 변경한다. 

<br>

<div>
  <div style="float:left; width:50%;">
    <img src="https://github.com/duohui12/blog/blob/main/images/cicd/nginx_conf_blue.png?raw=true">
  </div>
  <div style="float:left; width:50%;">
    <img src="https://github.com/duohui12/blog/blob/main/images/cicd/nginx_conf_green.png?raw=true"> 
  </div>
</div>
<div style="clear:both">  
</div>


위 이미지는 nginx의 설정 파일이다. 왼쪽 이미지는 블루환경 운영중일때, 오른쪽 이미지는 그린환경을 운영중일때의 설정이다.

`/api/*` 경로로 들어오는 모든 요청은 8081 또는 8080 포트에서 돌고 있는 백엔드 컨테이너로 던지도록 proxy_pass를 설정했다. 그 외 나머지 요청은 nginx 웹서버 (`/home/nginx/projects/blue-vue-app` 또는 `/home/nginx/projects/green-vue-app`) 에서 서비스하도록 구성했다. 이렇게 nginx의 conf 파일을 수정 후 `sudo nginx -s reload` 명령어로 nginx를 다시 로드하면 빠르게 운영중인 트래픽을 스위칭 시킬 수 있다. 

<br><br>

### 4. CICD 구성해보기

이제 전체적인 CICD 흐름을 살펴보자. 아래 이미지에서는 전체적인 흐름을 살펴보기 위해 젠킨스/프론트엔드/백엔드 서버가 분리된 서버처럼 그렸지만,  사실 하나의 우분투 서버 안에 컨테이너로 격리시켜 간단하게 구성했다.

![cicd.png](https://github.com/duohui12/blog/blob/main/images/cicd/cicd.png?raw=true)

1. 소스코드를 변경하고 원격 저장소로 push 할때, webhook으로 젠킨스 빌드가 시작된다.
2. 젠킨스 서버에서는 깃랩 원격 저장소에서 최신 소스를 가져와 빌드하고, 그 결과물을 프론트엔드/백엔드 서버에 전송한다. 
3. 프론트엔드/백엔드 서버에서 blue 또는 green 환경을 구성한다.
4. nginx의 conf 파일을 수정하고, reload 한다
5. 사용자가 `http://mydomain` 로 요청을 보내면 , nginx가 요청을 받아 서비스 중인 파일을 (html, css, js) 응답해준다. 
6. 브라우저에서 axios를 사용해 `http://mydomain/api/hello` 경로로 데이터를 요청하면, nginx가 먼저 요청을 받고 reverse proxy 역할을 수행해 백엔드 서버로 요청을 전달해준다.

<br><br>

### 5. 젠킨스 파이프라인 스크립트

젠킨스 파이프라인 각 stage의 스크립트이다.

<br>

1. 깃랩 원격 저장소에서 최신 소스를 가져온다.

```shell
stage('Checkout') {
    steps {
        git branch: "${env.GIT_BRANCH}"
                    , url: "${env.GIT_REPO_URL}"
                    , credentialsId:  "${env.GIT_CREDENTIALS_ID}"
    }
}
```

<br>

2. 현재 nginx에서 가리키고 있는 백엔드 컨테이너 포트와, 프론트엔드 폴더를 조회한다.

```shell
stage('Get Currently Running Backend Port') {
    steps {
        script {
            def port = sshagent(credentials: [env.NGINX_SERVER_SSH_CREDENTIALS_ID]) {

                def cmd = "ssh ${env.NGINX_SERVER_USER}@${env.NGINX_SERVER} -p ${env.NGINX_SSH_PORT} " + 
                          "'grep -oP \"proxy_pass\\s+https?://[^:]+:\\K\\d+\" /etc/nginx/conf.d/default.conf'"

                def output = sh(script: cmd, returnStdout: true).trim()

                return output
            }

            prev_backend_port = "${port}"
            echo "${env.ECHO_PREFIX} Currently Running Backend Port : ${prev_backend_port}"
        }
    }
}
```

```shell
stage('Get Currently Running Frontend Folder') {
     steps {
        script {
            def folder = sshagent(credentials: [env.NGINX_SERVER_SSH_CREDENTIALS_ID]) {

                def cmd = "ssh ${env.NGINX_SERVER_USER}@${env.NGINX_SERVER} -p ${env.NGINX_SSH_PORT} " + 
                          "'grep -oP \"root\\s+[^;]+/([^/;]+)/?;\" /etc/nginx/conf.d/default.conf | grep -oP \"[^/]+(?=/?;)\"'"

                def output = sh(script: cmd, returnStdout: true).trim()

                return output
            }

            prev_frontend_folder = "${folder}"
            echo "${env.ECHO_PREFIX} Currently Running Frontend Folder : ${prev_frontend_folder}"
        }
    }
}
```

<br>

3. 스프링부트 프로젝트 빌드 -> 배포서버로 전송 -> green 또는 blue 컨테이너 생성

```shell
#빌드
stage('Build') {
    steps {
        echo "${env.ECHO_PREFIX_BACKEND} Build" 
        sh """
            chmod +x ./gradlew
            ./gradlew clean build
        """
    }
}
```

```shell
#SCP 프로토콜로 빌드 결과물 배포 서버로 전송
stage('Copy jar to Backend Server using SCP Protocol') {
    steps {
        script {

            def jarFile = sh(script: 'ls build/libs/*.jar', returnStdout: true).trim()
            echo "${env.ECHO_PREFIX_BACKEND} JAR file: ${jarFile}"

            sshagent(credentials: [env.BACKEND_SERVER_SSH_CREDENTIALS_ID]) {
                sh "scp ${jarFile} ${env.BACKEND_SERVER_USER}@${env.BACKEND_SERVER}:${env.BACKEND_DEPLOY_DIR}"
            }

        }
    }
}
```

```shell
#배포할 서버에 현재 green 버전이 서비스 중이면 blue 버전 도커 이미지와 컨테이너 생성
#배포할 서버에 현재 blue 버전이 서비스 중이면 green 버전 도커 이미지와 컨테이너 생성  
stage('Create Green or Blue Docker Image and Container') {
    steps {
        script {

            if(prev_backend_port == env.GREEN_PORT){ 

                //green(8080) to blue(8081)
                prev_app_name = "${env.GREEN_APP_NAME}"
                new_app_name = "${env.BLUE_APP_NAME}"
                new_backend_port = "${env.BLUE_PORT}"

            }else{

                //blue(8081) to green(8080)
                prev_app_name = "${env.BLUE_APP_NAME}"
                new_app_name = "${env.GREEN_APP_NAME}"
                new_backend_port = "${env.GREEN_PORT}"

            }

            sshagent(credentials: [env.BACKEND_SERVER_SSH_CREDENTIALS_ID]) {
                sh """
                    ssh ${env.BACKEND_SERVER_USER}@${env.BACKEND_SERVER} << 'ENDSSH'

                    cd ${env.BACKEND_DEPLOY_DIR}

                    docker stop ${new_app_name}
                    docker rm ${new_app_name}
                    docker rmi ${new_app_name}
                    docker build -t ${new_app_name} .

                    docker run -d -p ${new_backend_port}:8080 --name ${new_app_name} ${new_app_name}

ENDSSH
                """
            }     


        }
    }
}
```

<br>

4. vue.js 프로젝트 빌드 -> 배포서버로 전송 -> green 또는 blue 폴더 생성

```shell
#빌드
stage('Build') {
    steps {
        echo "${env.ECHO_PREFIX_FRONTEND} Build" 

        sh """
            cd src/main/vue
            npm install
            npm run build
        """
    }
}
```

```shell
#SCP 프로토콜로 빌드 결과물 배포 서버로 전송
stage('Copy dist folder to Nginx Server using SCP Protocol'){
    steps {
        script {
            echo "${env.ECHO_PREFIX_FRONTEND} Copy Dist Folder To Nginx" 

            sshagent(credentials: [env.NGINX_SERVER_SSH_CREDENTIALS_ID]) {
                sh """
                    cd src/main/vue
                    scp -P ${env.NGINX_SSH_PORT} -r dist ${env.NGINX_SERVER_USER}@${env.NGINX_SERVER}:${env.NGINX_DEPLOY_DIR}
                """
          }
        }
    }
}
```

```shell
stage('Create Green or Blue Folder'){
    steps {
        script {

            if(prev_frontend_folder == env.GREEN_FOLDER){
                new_frontend_folder = "${env.BLUE_FOLDER}"

            }else {
                new_frontend_folder = "${env.GREEN_FOLDER}"
            }

            sshagent(credentials: [env.NGINX_SERVER_SSH_CREDENTIALS_ID]) {
                 sh """
                    ssh ${env.NGINX_SERVER_USER}@${env.NGINX_SERVER} -p ${env.NGINX_SSH_PORT} << 'ENDSSH'

                    cd /home/nginx/projects/
                    rm -rf ${new_frontend_folder}/*
                    cp -r vue/dist/* ${new_frontend_folder}
                    rm -rf vue/* 
ENDSSH
                """
          }
        }
    }
}
```

<br>

5. nginx 설정 변경 & reload

```shell
stage('Update Nginx Conf and Reload') {
    steps {
        script {
            echo "${env.ECHO_PREFIX_FRONTEND} Update Nginx Conf and Reload" 

            sshagent(credentials: [env.NGINX_SERVER_SSH_CREDENTIALS_ID]) {
                sh """
                    ssh ${env.NGINX_SERVER_USER}@${env.NGINX_SERVER} -p ${env.NGINX_SSH_PORT} << 'ENDSSH'
                    cd /etc/nginx/conf.d

                    export BACK_ACTIVE_PORT=${new_backend_port}
                    export FRONT_ACTIVE_PATH=${new_frontend_folder}
                    envsubst '\$BACK_ACTIVE_PORT \$FRONT_ACTIVE_PATH' < default.conf.template > default.conf


                    sudo nginx -s reload
ENDSSH
                """
            }

        }
    }
}
```

<br>

6. 이전 버전 컨테이너 stop 

```shell
stage('Stop Non Active Container') {
    steps {
        script {
            echo "${env.ECHO_PREFIX_BACKEND} STOP Non Active SpringBootApp Container" 

             sshagent(credentials: [env.BACKEND_SERVER_SSH_CREDENTIALS_ID]) {
                sh """
                    ssh ${env.BACKEND_SERVER_USER}@${env.BACKEND_SERVER} << 'ENDSSH'

                        docker stop ${prev_app_name} 
ENDSSH
                     """
            }     

        }
    }
}
```

<br><br>

### 6. 정리

현재 우리팀의 서비스는 닷넷,C#으로 개발하고 윈도우 서버를 사용하고 있다. 그리고 윈도우 서버용 웹서버인 iis를 사용해 서비스를 호스팅한다. 차세대 프로젝트를 진행하게 되면서 현재 테스트용으로 springboot+vue 애플리케이션을 개발중인데, 앞으로 어떤식으로 배포를 진행할지는 잘 모르겠다. 윈도우 서버와는 별개로 리눅스 서버를 사용할 가능성이 크지만, 만약 윈도우 서버에 간단한 테스트 서비스를 운영하게 된다면.. 위에서 NGINX가 했던 역할을 IIS에서 담당하도록 구성해볼 수 도 있을것 같다. (아직 테스트는 해보지 않았음) 

오늘 정리해본 CICD 프로세스는 가장 간단한 버전으로 아직 개선해야할 부분이 많다. 다음 버전에서는 아래와 같은 내용을 보완해볼 예정이다.

- 컨테이너 오케스트레이션 도구 사용해서 여러대의 운영서버에 배포해보기 
- 현재는 프론트 페이지만 수정해도 백엔드 컨테이너까지 다시 빌드하고, 배포되는 구조인데 좀 더 효율적으로 배포할 수 있는 방법 생각해보기
- green /blue 환경 스위칭 전에 헬스체크하는 기능 추가하기





